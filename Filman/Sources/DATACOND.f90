!DEC$ FREEFORM 
! DATACOND.f90 - SUBROUTINES FOR DATA CONDITIONING OPERATIONS SUCH
! AS DETRENDING, 4NN LAPLACIAN, & RE-REFERENCING.
!
!**********
! SUBROUTINE TO REMOVE MEAN & TREND, ADAPTED FROM BLOOMFIELD P.115
SUBROUTINE DETRND(X,N,K)
! SERIES X, LENGTH N, K=1 FOR MEAN & 2 FOR MEAN & TREND
	DIMENSION X(N)
	SUMX=0.
	FN=FLOAT(N)
	DO 20 I=1,N
20	    SUMX=SUMX+X(I)
	XBAR=SUMX/FN
	DO 30 I=1,N
30	    X(I)=X(I)-XBAR
	IF(K.LE.1)RETURN
	TBAR=(FN+1.)/2.
	SUMTT=FN*(FN*FN-1)/12.
	SUMTX=0.
	DO 40 I=1,N
40	    SUMTX=SUMTX+X(I)*(FLOAT(I)-TBAR)
	BETA=SUMTX/SUMTT
	DO 50 I=1,N
50	    X(I)=X(I)-BETA*(FLOAT(I)-TBAR)
	RETURN
END
! ****************
! SUBROUTINE TO CALCULATE ARBITRARY LINEAR TRANSFORM OF THE INPUT
! DATA, GIVEN A WEIGHTS MATRIX EXPRESSING EACH OUTPUT CHANNEL AS A
! LINEAR COMBINATION OF THE INPUTS. EXAMPLES ARE LAPLACIANS AND
! RE-REFERENCING OPERATIONS. ASSUMES # OUTPUTS .LE. # INPUTS.
SUBROUTINE MMPY(X,NPT,NCIN,NCOUT,XFM)
	DIMENSION X(NPT,NCIN),XFM(NCIN,NCOUT)
	REAL, ALLOCATABLE :: Y(:,:)
! DO MATRIX MPY: Y=X*XFM, USING Y AS TEMPORARY WORKSPACE
	ALLOCATE(Y(NPT,NCOUT),STAT=IERR)
	DO 45 J=1,NCOUT
	    DO 45 I=1,NPT
	        VAL=0.
	        DO 40 K=1,NCIN
40	            VAL=VAL+X(I,K)*XFM(K,J)
45	        Y(I,J)=VAL
! COPY RESULTS BACK INTO X
	DO 60 J=1,NCOUT
	    DO 60 I=1,NPT
60	        X(I,J)=Y(I,J)
	DEALLOCATE(Y)
	RETURN
	END
! *******************
! SUBROUTINE TO GENERATE A WEIGHTS MATRIX FOR THE 8NN LAPLACIAN FROM
! A MONTAGE FILE. SEE NUNEZ BOOK CHAPTER 6 FOR RATIONALE. ASSUMES ALL 
! RECORDED CHANNELS WILL BE PROCESSED. OUTPUT MATRIX IS NC x NC, WITH
! EACH COLUMN CONTAINING THE WEIGHTS FOR THE XFORM OF THE CORRESPONDING
! INPUT CHANNEL.
	SUBROUTINE MAKE8NN(MNTFIL,NC,XFM)
	CHARACTER*24 MNTFIL,ICLAB,INFIL
	DIMENSION XFM(NC,NC),ICHANS[ALLOCATABLE](:,:)
	DO 50 J=1,NC
	    DO 50 I=1,NC
50	        XFM(I,J)=0.
	ALLOCATE (ICHANS(NC,2),STAT=IERR)
! OPEN THE MONTAGE FILE
	L=LEN_TRIM(MNTFIL)
	INFIL=MNTFIL(1:L)//'.MNT'
	OPEN(UNIT=22,FILE=INFIL,ERR=1)
	GO TO 5
! ERROR CONDITIONS
1	WRITE(*,'(A)') ' CAN"T OPEN MONTAGE FILE'
	RETURN
2	WRITE(*,'(A)') ' ERROR READING MONTAGE FILE'
	RETURN
5	DO 10 I=1,NC
	    READ(22,100,ERR=2,END=10)IC,ICLAB
100	    FORMAT(I2/A24)    ! TAKEN FROM EKHELM.PRM
	    IF(IC.EQ.I) GO TO 8
	    WRITE(*,'(A)') ' MONTAGE FILE NOT IN SEQUENCE'
	    RETURN
8	    CALL READLAB(ICLAB,IROW,ICOL)
	    ICHANS(IC,1)=IROW
	    ICHANS(IC,2)=ICOL
10	CONTINUE
! GENERATE XFM FROM ICHANS, ONE COLUMN AT A TIME
	DO 30 J=1,NC
	    XFM(J,J)=1.0      ! ALWAYS
	    TOT=0.0
	    IR=ICHANS(J,1)
	    IF(IR.EQ.0)GO TO 30     ! NO XFM FOR NON-SCALP CHANNELS
	    IC=ICHANS(J,2)
! FIND THE 1-STEP NEIGHBORS AND SUM THEIR DISTANCES FROM TARGET
	    DO 20 I=1,NC
	        IF(I-J)11,20,11   ! SKIP THE TARGET ITSELF
11	        IF(ICHANS(I,1))20,20,12  ! SKIP NON-SCALP CHANNELS
12	        DX=ABS(ICHANS(I,1)-IR)
	        IF(DX .GT. 1.)GO TO 20  ! NOT A NEIGHBOR
	        DY=ABS(ICHANS(I,2)-IC)
	        IF(DY .GT. 1.)GO TO 20  !  "  "    " 
	        RWT=1.0/SQRT(DX*DX + DY*DY)   ! RELATIVE WT, PROP. TO DIST**-1
	        TOT=TOT+RWT
	        XFM(I,J)=-RWT    ! PLACEHOLDER; NORMALIZED TO FINAL VALUE BELOW
20	        CONTINUE
! NOW NORMALIZE THIS COLUMN'S WEIGHTS BASED ON #'S AND TYPES FOUND
	    DO 25 I=1,NC
	        IF(XFM(I,J))21,25,25
21	        XFM(I,J)=XFM(I,J)/TOT   ! SUM OF WEIGHTS = -1.0
25	        CONTINUE
30	    CONTINUE
	DEALLOCATE(ICHANS)
	RETURN
END
! ***************
! LINCOMB.FOR- GENERATES A MATRIX OF COEFFICIENTS FOR USER-SPECIFIED
! LINEAR COMBINATIONS OF THE INPUT CHANNELS. CURRENTLY SUPPORTED
! OPTIONS ARE: (a) RE-REFERENCING OF SCALP CHANNELS (ONLY!) TO AN 
! ALTERNATE COMMON REFERENCE, AND (b) ARBITRARILY WEIGHTED COMBINATIONS
! OF ANY OF THE INPUT CHANNELS. ANY RELABELING OF THE OUTPUT CHANNELS
! MUST BE HANDLED BY THE CALLING ROUTINE, AND NCOUT MUST BE .LE. NCIN.
SUBROUTINE LINCOMB(MNTFIL,NCIN,NCOUT,XFM)
	CHARACTER*24 MNTFIL,ICLAB,INFIL,TEMP
	CHARACTER*5 LABELS(64)
	CHARACTER*80 LCOMB,BLANK80
	CHARACTER*1 BLANKS(80)
	CHARACTER*11 FMT  ! MODIFIABLE FORMAT FOR READING CHAN/WGHT SPECS
	DIMENSION XFM(NCIN,NCIN),ICHANS[ALLOCATABLE](:,:)
	EQUIVALENCE (BLANK80,BLANKS)
	DATA FMT/'(I1,1XF6.0)'/
	DATA BLANKS/80*' '/
	DO 50 J=1,NCIN
	    DO 50 I=1,NCIN
50	    XFM(I,J)=0.
	ALLOCATE (ICHANS(NCIN,2),STAT=IERR)
! OPEN THE MONTAGE FILE
	L=LEN_TRIM(MNTFIL)
	INFIL=MNTFIL(1:L)//'.MNT'
	OPEN(UNIT=22,FILE=INFIL,ERR=1)
	GO TO 5
! ERROR CONDITIONS
1	WRITE(*,'(A)') ' CAN"T OPEN MONTAGE FILE'
	RETURN
2	WRITE(*,'(A)') ' ERROR READING MONTAGE FILE'
	RETURN
5	DO 10 I=1,NCIN
        READ(22,99,ERR=2,END=10)IC,ICLAB
99	    FORMAT(I2/A24)    ! TAKEN FROM EKHELM.PRM
	    IF(IC.EQ.I)GO TO 8
	    WRITE(*,'(A)') ' MONTAGE FILE NOT IN SEQUENCE'
	    RETURN
8	    CALL READLAB(ICLAB,IROW,ICOL)
	    ICHANS(IC,1)=IROW
	    ICHANS(IC,2)=ICOL
	    LABELS(IC)=ICLAB(1:5)
10	CONTINUE
! FIND OUT WHAT TO DO
	WRITE(*,100)
100	FORMAT('$OPTION:1=RE-REFERENCING/2=OTHER RECOMBINATIONS >'\)
	READ(*,*)MODE
	WRITE(*,101)(I,LABELS(I),I=1,NCIN)
101	FORMAT(8(1XI2,'=',A5))
	GO TO(20,30)MODE
! RE-REFERENCING; NOTE THAT THE NEW REFERENCE CHANNEL IS ZEROED
20	NCOUT=NCIN
	WRITE(*,200)
200	FORMAT('$ENTER CHANNEL NUMBER OF NEW SCALP REFERENCE >'\)
	READ(*,*)NEWREF
22	DO 25 I=1,NCOUT
	    XFM(I,I)=1.0
	    IF(ICHANS(I,1).EQ.0)GO TO 25  ! DON'T RE-REF NON-SCALP CHANNELS
	    XFM(NEWREF,I)=XFM(NEWREF,I)-1.0     ! ZEROES NEWREF
25	    CONTINUE      
	GO TO 90
! ARBITRARY RECOMBINATIONS
30	WRITE(*,300)NCIN
300	FORMAT('$NUMBER OF COMBINATIONS (.LE.',I2,') >'\)
	READ(*,*)NCOUT
	IF(NCOUT.GT.NCIN)GO TO 30
	WRITE(*,301)
301	FORMAT('$ENTER EACH IN 1-LINE FORMAT:[<NC1,WT1>...<NCK,WTK>]')
	DO 60 J=1,NCOUT
31	    LCOMB=BLANK80
	    READ(*,'(A80)')LCOMB
! PROCESS THE STRING BACK TO FRONT
	    L0=INDEX(LCOMB,']')
35	    L1=L0-2
	    L2=INDEX(LCOMB(1:L1),'<',.TRUE.)
	    IF(L2.NE.0)GO TO 36
32	    WRITE(*,'(A)') ' ERROR IN SPECIFIER; RE-ENTER IT'
	    GO TO 31
36	    TEMP=LCOMB(L2+1:L1)
	    LL=LEN_TRIM(TEMP)
	    I=INDEX(TEMP,',')
	    IF(I.EQ.0)GO TO 32
	    IF(INDEX(TEMP,'>').NE.0)GO TO 32
! MODIFY FORMAT FOR INTERNAL READ
	    WRITE(FMT(3:3),'(I1)')I-1
	    WRITE(FMT(8:8),'(I1)')LL-I
	    READ(TEMP,FMT)NC,WEIGHT
	    XFM(NC,J)=WEIGHT
	    L0=L2
	    IF(L0.GT.2)GO TO 35     ! CHANGE IF WE CHANGE SPEC FORMAT
60	    CONTINUE
90	DEALLOCATE(ICHANS)
	RETURN
END
