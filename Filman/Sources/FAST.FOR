C FAST.FOR - IEEE RADIX 4/2 FFT ROUTINE
C REPLACES THE REAL VECTOR B(K), FOR K=1,2,...,N
C WITH DISCRETE FOURIER TRANSFORM. DC TERM IS IN B(1), B(2)=0,
C JTH HARMONIC RETURNED AS COMPLEX NUMBER STORED IN B(2*J+1),B(2*J+2),
C AND N/2 STORED IN B(N+1) WITH B(N+2)=0. NOTE THAT B MUST BE 
C DIMENSIONED TO N+2 AND N MUST BE A POWER OF 2.
C MODIFIED FOR USE WITH FILMAN & SPECTR BY EFK, 1991

	SUBROUTINE FAST(B,N)
	DIMENSION B(*)
	COMMON/CONS/ PII,P7,P7TWO,C22,S22,PI2
	COMMON/DEV/ITI
	PII=4.*ATAN(1.)
	PI8=PII/8.
	P7=1./SQRT(2.)
	P7TWO=2.*P7
	C22=COS(PI8)
	S22=SIN(PI8)
	PI2=2.*PII
! Must have data size of power of 2, less than or equal to 2^16 (65536) points
	DO 10 I=1,16
	  M=I
	  NT=2**I
	  IF(N.EQ.NT)GO TO 20
10    CONTINUE
      Call ShowInfoText('Warning','N NOT A POWER OF 2 FOR FAST')
	STOP
20    N4POW=M/2

C RADIX 2 ITERATION FIRST IF NECESSARY

	IF(M-N4POW*2)40,40,30
30    NN=2
	INT=N/NN
	CALL FR2TR(INT,B(1),B(INT+1))
	GO TO 50
40    NN=1

C PERFORM RADIX 4 ITERATIONS

50    IF(N4POW.EQ.0)GO TO 70
	DO 60 IT=1,N4POW
	NN=NN*4
      INT=N/NN
	CALL FR4TR(INT,NN,B(1),B(INT+1),B(2*INT+1),
     1  B(3*INT+1),B(1),B(INT+1),B(2*INT+1),B(3*INT+1))
60    CONTINUE

C PERFORM IN-PLACE REORDERING

70    CALL FORD1(M,B)
	CALL FORD2(M,B)
	T=B(2)
	B(2)=0.
	B(N+1)=T
	B(N+2)=0.
	DO 80 IT=4,N,2
	  B(IT)=-B(IT)
80    CONTINUE
	RETURN
	END

C RADIX 2 ITERATION SUBROUTINE

	SUBROUTINE FR2TR(INT,B0,B1)
	DIMENSION B0(*),B1(*)
	DO 10 K=1,INT
	T=B0(K)+B1(K)
	B1(K)=B0(K)-B1(K)
	B0(K)=T
10    CONTINUE
	RETURN
	END

C RADIX 4 ITERATION SUBROUTINE

	SUBROUTINE FR4TR(INT,NN,B0,B1,B2,B3,B4,B5,B6,B7)
	DIMENSION L(15),B0(*),B1(*),B2(*),B3(*),B4(*),B5(*),B6(*),B7(*)
	COMMON/CONS/PII,P7,P7TWO,C22,S22,PI2
	EQUIVALENCE (L15,L(1)),(L14,L(2)),(L13,L(3)),(L12,L(4)),
     1   (L11,L(5)),(L10,L(6)),(L9,L(7)),(L8,L(8)),(L7,L(9)),
     2   (L6,L(10)),(L5,L(11)),(L4,L(12)),(L3,L(13)),(L2,L(14)),
     3   (L1,L(15))

C JTHET IS REVERSED BINARY COUNTER, JR STEPS 2ATATIME TO LOCATE REAL
C PARTS AND JI LOCATES CORRESPONDING IMAGINARY PART

	L(1)=NN/4
	DO 40 K=2,15
	  IF(L(K-1)-2)10,20,30
10      L(K-1)=2
20      L(K)=2
	  GO TO 40
30      L(K)=L(K-1)/2
40    CONTINUE
	PIOVN=PII/FLOAT(NN)
	JI=3
	JL=2
	JR=2
	DO 120 J1=2,L1,2
	DO 120 J2=J1,L2,L1
	DO 120 J3=J2,L3,L2
	DO 120 J4=J3,L4,L3
	DO 120 J5=J4,L5,L4
	DO 120 J6=J5,L6,L5
	DO 120 J7=J6,L7,L6
	DO 120 J8=J7,L8,L7
	DO 120 J9=J8,L9,L8
	DO 120 J10=J9,L10,L9
	DO 120 J11=J10,L11,L10
	DO 120 J12=J11,L12,L11
	DO 120 J13=J12,L13,L12
	DO 120 J14=J13,L14,L13
	DO 120 JTHET=J14,L15,L14
	  TH2=JTHET-2
	  IF(TH2)50,50,90
50      DO 60 K=1,INT
	    T0=B0(K)+B2(K)
	    T1=B1(K)+B3(K)
	    B2(K)=B0(K)-B2(K)
	    B3(K)=B1(K)-B3(K)
	    B0(K)=T0+T1
	    B1(K)=T0-T1
60    CONTINUE
	IF(NN-4)120,120,70
70    K0=INT*4+1
	KL=K0+INT-1
	DO 80 K=K0,KL
	  PR=P7*(B1(K)-B3(K))
	  PI=P7*(B1(K)+B3(K))
	  B3(K)=B2(K)+PI
	  B1(K)=PI-B2(K)
	  B2(K)=B0(K)-PR
	  B0(K)=B0(K)+PR
80    CONTINUE
	GO TO 120
90    ARG=TH2*PIOVN
	C1=COS(ARG)
	S1=SIN(ARG)
	C2=C1**2-S1**2
	S2=C1*S1+C1*S1
	C3=C1*C2-S1*S2
	S3=C2*S1+S2*C1
	INT4=INT*4
	J0=JR*INT4+1
	K0=JI*INT4+1
	JLAST=J0+INT-1
	DO 100 J=J0,JLAST
	  K=K0+J-J0
	  R1=B1(J)*C1-B5(K)*S1
	  R5=B1(J)*S1+B5(K)*C1
	  T2=B2(J)*C2-B6(K)*S2
	  T6=B2(J)*S2+B6(K)*C2
	  T3=B3(J)*C3-B7(K)*S3
	  T7=B3(J)*S3+B7(K)*C3
	  T0=B0(J)+T2
	  T4=B4(K)+T6
	  T2=B0(J)-T2
	  T6=B4(K)-T6
	  T1=R1+T3
	  T5=R5+T7
	  T3=R1-T3
	  T7=R5-T7
	  B0(J)=T0+T1
	  B7(K)=T4+T5
	  B6(K)=T0-T1
	  B1(J)=T5-T4
	  B2(J)=T2-T7
	  B5(K)=T6+T3
	  B4(K)=T2+T7
	  B3(J)=T3-T6
100   CONTINUE
	JR=JR+2
	JI=JI-2
	IF(JI-JL)110,110,120
110   JI=2*JR-1
	JL=JR
120   CONTINUE
	RETURN
	END

C IN-PLACE REORDERING SUBROUTINE

	SUBROUTINE FORD1(M,B)
	DIMENSION B(*)
	K=4
	KL=2
	N=2**M
	DO 40 J=4,N,2
	  IF(K-J)20,20,10
10      T=B(J)
	  B(J)=B(K)
	  B(K)=T
20      K=K-2
	  IF(K-KL)30,30,40
30      K=2*J
	  KL=J
40    CONTINUE
	RETURN
	END

C IN-PLACE REORDERING SUBROUTINE

	SUBROUTINE FORD2(M,B)
	DIMENSION L(15),B(*)
	EQUIVALENCE (L15,L(1)),(L14,L(2)),(L13,L(3)),(L12,L(4)),
     1   (L11,L(5)),(L10,L(6)),(L9,L(7)),(L8,L(8)),(L7,L(9)),
     2   (L6,L(10)),(L5,L(11)),(L4,L(12)),(L3,L(13)),(L2,L(14)),
     3   (L1,L(15))

	N=2**M
	L(1)=N
	DO 10 K=2,M
	L(K)=L(K-1)/2
10    CONTINUE
	DO 20 K=M,14
	L(K+1)=2
20    CONTINUE
	IJ=2
	DO 40 J1=2,L1,2
	DO 40 J2=J1,L2,L1
	DO 40 J3=J2,L3,L2
	DO 40 J4=J3,L4,L3
	DO 40 J5=J4,L5,L4
	DO 40 J6=J5,L6,L5
	DO 40 J7=J6,L7,L6
	DO 40 J8=J7,L8,L7
	DO 40 J9=J8,L9,L8
	DO 40 J10=J9,L10,L9
	DO 40 J11=J10,L11,L10
	DO 40 J12=J11,L12,L11
	DO 40 J13=J12,L13,L12
	DO 40 J14=J13,L14,L13
	DO 40 JI=J14,L15,L14
	  IF(IJ-JI)30,40,40
30      T=B(IJ-1)
	  B(IJ-1)=B(JI-1)
	  B(JI-1)=T
	  T=B(IJ)
	  B(IJ)=B(JI)
	  B(JI)=T
40      IJ=IJ+2
	RETURN
	END



